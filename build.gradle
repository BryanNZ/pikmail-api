group 'com.nearsoft'
version '1.0.3'

buildscript {
    ext.kotlin_version = '1.2.51'
    ext.ktor_version = '0.9.3'

    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'org.jetbrains.ktor.netty.DevelopmentHost'

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "com.github.epool:pikmail:1.0.2"
    implementation "com.github.epool:ip-api-klient:1.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:0.23.4"
    implementation "com.mixpanel:mixpanel-java:1.4.4"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.nearsoft.pikmail.api.Main'
        )
    }
//    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
//    or
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir
    rename {
        'pikmail-api.jar'
    }
}
stage.mustRunAfter(clean)

clean.doLast {
    project.file('pikmail-api.jar').delete()
}